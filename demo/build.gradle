apply plugin: 'com.android.application'

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    new FileInputStream(localPropertiesFile).withCloseable { stream -> localProperties.load(stream) }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "lens24.demo"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode Integer.parseInt(DEMO_APP_VERSION_CODE)
        versionName DEMO_APP_VERSION_NAME
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs 'en'
        multiDexEnabled = true
    }
    signingConfigs {
        release
        debug
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue 'string', "adUnitId", localProperties.getProperty('adUnitId', '')
            resValue 'string', "adMobApiKey", localProperties.getProperty('adMobApiKey', '')
        }
        debug {
            minifyEnabled false
            jniDebuggable true
            resValue 'string', "adUnitId", localProperties.getProperty('adUnitIdDebug', 'ca-app-pub-3940256099942544/6300978111')
            resValue 'string', "adMobApiKey", localProperties.getProperty('adMobApiKeyDebug', 'ca-app-pub-3940256099942544~3347511713')
        }
        profiling {
            initWith debug
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE.txt', 'META-INF/NOTICE.txt']
        }
    }

    namespace = 'demo'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ":sdk")
    implementation "androidx.appcompat:appcompat:$rootProject.ext.appCompatVersion"
    implementation "com.google.android.material:material:$rootProject.ext.materialVersion"
    implementation "com.airbnb.android:lottie:$rootProject.ext.lottieVersion"
    implementation "com.google.android.gms:play-services-ads-lite:$rootProject.ext.adsVersion"
}
