plugins {
    id 'com.android.library'
    id 'com.vanniktech.maven.publish'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    ndkVersion = "26.3.11579264"

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                abiFilters 'armeabi-v7a', "arm64-v8a", /*"x86",*/  "x86_64"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled = true
            jniDebuggable = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-customer.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    resourcePrefix = "lens24_"
    namespace = 'lens24.sdk'
}

dependencies {
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.ext.constraintlayoutVersion"
    implementation "androidx.appcompat:appcompat:$rootProject.ext.appCompatVersion"
    implementation "com.airbnb.android:lottie:$rootProject.ext.lottieVersion"
}

mavenPublishing {
    coordinates("io.github.vlasentiy", "lens24", "1.1.3")
    signAllPublications()
    publishToMavenCentral()

    pom {
        name = "lens24"
        description = "ðŸ“· Library for scanning and OCR of credit or payment cards"
        inceptionYear = "2022"
        url = "https://github.com/vlasentiy/Lens24"
        licenses {
            license {
                name = "MIT License"
                url = "https://github.com/vlasentiy/Lens24/blob/master/LICENSE.md"
            }
        }
        developers {
            developer {
                id = "vlas"
                name = "Vlas Artanov"
                url = "https://github.com/vlasentiy"
            }
        }
        scm {
            connection = "scm:git:git://github.com/vlasentiy/Lens24.git"
            developerConnection = "scm:git:ssh://git@github.com/vlasentiy/Lens24.git"
            url = "https://github.com/vlasentiy/Lens24/tree/master"
        }
    }
}
